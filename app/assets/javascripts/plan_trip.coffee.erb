#anything code related to trips/new and trips/edit should be put here.

markers = []
currentMarkerID = 0

$ ->

  #on click of the button, save the current trip
  $('#save-trip').click ->
    tripTitle = $('#trip_name_input').val()
    saveTrip(tripID, tripTitle)

  #remove button on infobox should remove pin and path
  $('body').on 'click', '.remove-location', ->
    markerID = $(this).parent().data('marker-id')
    removeMarker(markerID, map)

  $(document).bind 'cbox_complete', ->
    initSearch() #calling initSearch because initial call didn't bind to lightbox input
    $('.add-destination-input').focus()

    # PRE: place should be defined from the autocomplete function
    # PRE: map should be initialized
    $('#add-destination-submit').click ->
      if $('.add-destination-input').val() == ''
        $('.lightbox-warning').remove()
        content = $('.lightbox-warning-template').clone().removeClass('hidden')
        content.removeClass('.lightbox-warning-template').addClass('lightbox-warning')
        content.find('#lightbox-warning-content').html('Please enter a destination.')
        content.insertBefore('#add-destination-lightbox-wrapper')
        $.colorbox.resize()
      else
        setTimeout ->
          $('.lightbox-warning').remove()
          content = $('.lightbox-warning-template').clone().removeClass('hidden')
          content.removeClass('.lightbox-warning-template').addClass('lightbox-warning')
          content.insertBefore('#add-destination-lightbox-wrapper')
          $.colorbox.resize()
        , 300

#generates unique marker ID for the page
generateMarkerID = ->
  return currentMarkerID++

# PRE: place should be defined from the autocomplete function
# PRE: map should be initialized
#Parent function that should be called when a new destination is added to the map
addDestination = (place, map) ->
  if place != null
    markerIndex = addPlaceToMap(place, map)
    response = saveDestinationToDatabase(place)
    destinationID = response.responseText
    addDestinationSnapshot(place, markerIndex, destinationID)


#save trip to database, destination and details should already be in the database
saveTrip = (trip_id, trip_title) ->
  $.ajax '/trips/create',
    dataType: 'json'
    type: 'POST'
    data:
      trip_title: trip_title
      trip_id: trip_id
    success: (data) ->
      alert 'Successful'
      return
    failure: ->
      alert 'Unsuccessful'
      return



#add location to trip snapshot
#vars: place = google place object
#      map = google map object
addDestinationSnapshot = (place, markerID, destinationID) ->
  destinationName = place.name
  snapshot = $('#snapshot-location-template').clone().removeClass('hidden').removeAttr('id')
  #setting data needed for trip
  snapshot.find('h5').text(place.name)
  snapshot.attr('data-marker-id', markerID)
  snapshot.attr('data-destination-id', destinationID)
  snapshot.insertBefore('#snapshot-add-destination')

  snapshot.find('.snapshot-location-content').click ->
    info_content = getInfowindowContent(place, markerID)
    infowindow.setContent(info_content)
    markerIndex = findMarkerIndexByID(markerID)
    infowindow.open(map, markers[markerIndex])



  snapshot.hover ->
    dragIndicator = $(this).find('.snapshot-draggable-indicator')
    dragIndicator.toggleClass('hidden')

    $(this).find('.snapshot-close').toggleClass('hidden')
    dragIndicator.hover ->
      dragIndicator.parent().toggleClass('snapshot-location-shadow')

  snapshot.find('.snapshot-close').click ->
    markerID = $(this).parent().data('marker-id')
    $(this).parent().remove()
    $('#trip-snapshot-link-ul li:first').remove()
    removeMarker(markerID, map)

  $('#trip-snapshot-link-ul').append('<li></li>')



#returns the formatted infowindow content
getInfowindowContent = (place, markerID) ->
  iw = $('#infowindow-template').clone(true).removeClass('hidden').removeAttr('id')
  iw.find('h5').text(place.formatted_address) #infowindow header
  iw.find('.infowindow-content').attr('data-marker-id', markerID)
  return  iw.html()

#adds a marker at the geolocation specified in place (google place),
#on the map specified in map, connects path to previous marker if necessary
#PRE: bounds array must be declared
#@return index of marker in markers array
addPlaceToMap = (place, map) ->
  markerID = generateMarkerID()
  marker = new google.maps.Marker
    position: place.geometry.location
    map: map
    title: place.formatted_address
    id: markerID

  #open infowindow on click
  marker.addListener 'click', ->
    infowindow.open(map, marker)

  #add position to the maps bounds
  bounds.extend marker.position

  markers.push marker
  markerIndex = markers.length-1 #marker index is last element or array
  polyline.getPath().setAt(markerIndex, marker.position);

  if markers.length == 1
    map.setCenter(marker.position)
    map.setOptions(zoom: 6)
  else
    map.fitBounds(bounds)

  #lastly, open window of place just added
  info_content = getInfowindowContent(place, markerID)
  infowindow.setContent(info_content)
  infowindow.open(map, marker)

  return markerID


#removes a marker with markerIndex from the map and markers array
removeMarker = (markerID, map) ->
  markerIndex = findMarkerIndexByID(markerID)
  marker = markers[markerIndex]
  marker.setMap(null)
  polyline.getPath().removeAt(markerIndex)
  markers.splice(markerIndex, 1);

#@RETURN: returns the index of marker with the given ID
findMarkerIndexByID = (markerID) ->
  for marker, i in markers
    if marker.id == markerID
      return i


#saves the destination to the database if it doesn't already exist
#@RETURN: destinationID of location
saveDestinationToDatabase = (place) ->
  $.ajax '/destinations',
    dataType: 'json'
    type: 'POST'
    data:
      place: JSON.stringify(place)
      trip_id: tripID
    success: (destinationID) ->
      return JSON.stringify(destinationID)
    failure: ->
      alert 'saveDestiantionToDatabase() Unsuccessful, please alert site admins'
      return

autocomplete = null
place = null
#autocomplete search box initialization
initSearch = ->
  autocomplete = new (google.maps.places.Autocomplete)(document.getElementById('location-query'), types: [ 'geocode' ])
  autocomplete.addListener 'place_changed', ->
    $('#lightbox-warning-template').hide()
    place = autocomplete.getPlace()
    addDestination(place, map)
    $.colorbox.close()
